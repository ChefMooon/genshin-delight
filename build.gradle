plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.8.4"
//    id "me.shedaniel.unified-publishing" version "0.1.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"
//    apply plugin: "me.shedaniel.unified-publishing"

    loom {
        silentMojangMappingsLicense()
    }

    repositories {
        maven { url "https://maven.parchmentmc.org" } // Parchment mappings

//        maven { url 'https://maven.neoforged.net/releases' } // NeoForge

        maven { url 'https://maven.greenhouseteam.dev/releases/' } // Farmers Delight Refabricated
        maven { url "https://mvn.devos.one/releases/" } // Porting Lib
        maven {
            url "https://maven.jamieswhiteshirt.com/libs-release" // Reach Entity Attributes (Required by Porting Lib)
            content { includeGroup "com.jamieswhiteshirt" }
        }
        maven {
            url "https://jitpack.io/" // Fabric ASM
            content { excludeGroup "io.github.fabricators_of_create" }
        }

        maven { url 'https://www.cursemaven.com' } // Farmers Delight Forge
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings(loom.layered {
            it.officialMojangMappings { nameSyntheticMembers = false }
            it.parchment("org.parchmentmc.data:parchment-${parchment_version}@zip")
        })
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    }

    tasks.processResources {
        var replaceProperties = [
                minecraft_version    : minecraft_version,
                minecraft_min_version: minecraft_min_version,
                minecraft_max_version: minecraft_max_version,

                mod_id               : project.mod_id,
                mod_name             : project.mod_name,
                mod_version          : project.mod_version,
                mod_license          : project.mod_license,
                mod_authors          : project.mod_authors,
                mod_description      : project.mod_description,
                mod_credits          : project.mod_credits,
                mod_homepage         : project.mod_homepage,
                mod_github           : project.mod_github,
                mod_issues           : project.mod_issues,
                mod_discord          : project.mod_discord
        ]

        inputs.properties replaceProperties

        filesMatching(["META-INF/mods.toml", "fabric.mod.json", "pack.mcmeta"]) {
            expand replaceProperties + [project: project]
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    def buildSuffix = mod_version_type != "release" ? "-${mod_version_type}" + (mod_build_number != "0" ? "-${mod_build_number}" : "") : ""

    version = "${project.name}-${rootProject.minecraft_version}-${rootProject.mod_version}${buildSuffix}"

    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
